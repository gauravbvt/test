package com.mindalliance.channels.playbook.rules

import com.mindalliance.channels.playbook.ref.*
import com.mindalliance.channels.playbook.ifm.*
import com.mindalliance.channels.playbook.ifm.definition.*
import com.mindalliance.channels.playbook.ifm.info.*
import com.mindalliance.channels.playbook.ifm.project.environment.*
import com.mindalliance.channels.playbook.ifm.playbook.*
import com.mindalliance.channels.playbook.analysis.*
import com.mindalliance.channels.playbook.analysis.profile.*
import java.util.*



##### Direct profile element generation by Information Acts ###########

// TODO set end durations where applicable

rule "detection"
    agenda-group "profiles"
	when
		$act: Detection($actor:actorAgent,  $information: information)
		not (exists Invalid(element == $act))
	then
	    Know know = new Know($act, $actor, $information);
	    insertLogical(know);  // adds knowledge of information to actor from start of act in act's playbook
	    log($act, know);
    end


rule "sharing act"
    agenda-group "profiles"
    when
        $act: SharingAct($actor:actorAgent, $target:targetAgent, $information: information)
		not (exists Invalid(element == $act))
    then
        Know know1 = new Know($act, $target, $information);
        insertLogical(know1);
        Know know2 = new Know($act, $actor, $information);
        insertLogical(know2);
        Insight insight1 = new Insight($act, $actor, know1);
        insertLogical(insight1);
        Insight insight2 = new Insight($act, $target, know2);
        log($act, know1);
        log($act, know2);
        log($act, insight1);
        log($act, insight2);
    end

rule "task"
    agenda-group "profiles"
    when
        $act: Task($actor:actorAgent, $needs:informationNeeds)
		not (exists Invalid(element == $act))
    then
        for (InformationNeed need : (List<InformationNeed>)$needs) {
            if (need.isDefined()) {
                NeedToKnow n2k = new NeedToKnow($act, $actor, need);
                insertLogical(n2k);
                log($act, n2k);
            }
        }
    end

rule "information request"
    agenda-group "profiles"
    when
        $act: InformationRequest($actor:actorAgent, $target:targetAgent, $needy:informationNeed.agent , $need:informationNeed)
		not (exists Invalid(element == $act))
    then
        NeedToKnow n2k = new NeedToKnow($act, (Ref)$needy, $need);
        insertLogical(n2k);
        Insight insight1 = new Insight($act, $actor, n2k);
        insertLogical(insight1);
        Insight insight2 = new Insight($act, $target, n2k);
        insertLogical(insight2);
        log($act, n2k);
        log($act, insight1);
        log($act, insight2);
    end

rule "assignation"
    agenda-group "profiles"
    when
        $act: Assignation($actor:actorAgent, $target:targetAgent, $responsibility:responsibility, $assignee:assigneeAgent)
		not (exists Invalid(element == $act))
    then
        Assignment assignment = new Assignment($act, $assignee, $responsibility);
        Insight insight1 = new Insight($act, $actor, assignment);
        insertLogical(insight1);
        Insight insight2 = new Insight($act, $target, assignment);
        insertLogical(insight2);
        log($act, insight1);
        log($act, insight2);
    end

rule "assignation received"
    agenda-group "profiles"
    when
        $act: Assignation($actor:actorAgent, $target:targetAgent, $responsibility: responsibility, $assignee:assigneeAgent,
                          (assigneeAgent == targetAgent || assigneeAgent == actorAgent))
		not (exists Invalid(element == $act))
    then
        Assignment assignment = new Assignment($act, $assignee, $responsibility);
        insertLogical(assignment);
        log($act, assignment);
    end

rule "association"
    agenda-group "profiles"
    when
        $act: Association($actor:actorAgent, $associate:toAgent, $name:relationshipName)
		not (exists Invalid(element == $act))
    then
        Relationship relationship = Relationship.create($actor, $name, $associate);
        Link link = new Link($act, $actor, relationship);
        insertLogical(link);
        log($act, link);
    end

rule "reverse association"
    agenda-group "profiles"
    when
        $act: Association($actor:actorAgent, $associate:toAgent, $name:reverseRelationshipName)
        eval(!$name.isEmpty())
		not (exists Invalid(element == $act))
    then
        Relationship relationship = Relationship.create($associate, $name, $actor);
        Link link = new Link($act, $associate, relationship);
        insertLogical(link);
        log($act, link);
    end

rule "confirmation request"
    agenda-group "profiles"
    when
        $act: ConfirmationRequest($actor:actorAgent, $target:targetAgent, $information:information, $sourceSpec:sourceSpec)
		not (exists Invalid(element == $act))
    then
        SourceNeed sourceNeed = new SourceNeed($act, $actor, $sourceSpec, $information);
        insertLogical(sourceNeed);
        log($act, sourceNeed);
        Insight insight = new Insight($act, $target, sourceNeed);
        log($act, insight);
    end

rule "relocation"
    agenda-group "profiles"
    when
        $act: Relocation($actor:actorAgent, $location:location)
		not (exists Invalid(element == $act))
    then
        Locality locality = new Locality($act, $actor, $location);
        insertLogical(locality);
        log($act, locality);
    end

rule "sharing request"
    agenda-group "profiles"
    when
        $act: SharingRequest($actor:actorAgent, $target:targetAgent, $protocol:protocol, $infoSpec:protocol.informationSpec)
		not (exists Invalid(element == $act))
    then
        InformationNeed need = InformationNeed.fromSharingProtocol($actor, $target, $protocol);
        NeedToKnow n2k = new NeedToKnow($act, $actor, need);
        insertLogical(n2k);
        log($act, n2k);
        Insight insight = new Insight($act, $target, n2k);
        insertLogical(insight);
        log($act, insight);
    end

rule "sharing commitment"
    agenda-group "profiles"
    when
        $act: SharingCommitment($actor:actorAgent, $target:targetAgent, $protocol:protocol, $constraints:constraints)
		not (exists Invalid(element == $act))
    then
        Commitment commitment = new Commitment($act, $actor, $target, $protocol, $constraints);
        insertLogical(commitment);
        log($act, commitment);
        Insight insight = new Insight($act, $target, commitment);
        insertLogical(insight);
        log($act, insight);
    end

### Insight gained by detection of Information Acts


### Termination of profile elements by Information Acts 
