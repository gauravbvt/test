<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
    <xsl:output method="xml" omit-xml-declaration="yes" indent="yes"/>
    <xsl:strip-space elements="*"/>
    <xsl:template match="/report">
        <html>
            <head>
                <title>
                    <xsl:value-of select="info/title"/>
                </title>
            </head>
            <body>
                <table class="report">
                    <thead>
                        <xsl:apply-templates select="info"/>
                    </thead>
                    <tbody>
                        <xsl:apply-templates select="index"/>
                        <xsl:apply-templates select="body/resource"/>
                    </tbody>
                    <tfoot>
                        <xsl:apply-templates select="footer"/>
                    </tfoot>
                </table>
            </body>
        </html>
    </xsl:template>

    <xsl:template match="info">
        <tr>
            <td>
                <span class="title">
                    <xsl:value-of select="title"/> for <xsl:value-of select="context"/>
                </span> &#160; <span class="info">generated by <xsl:value-of select="user"/> on
                        <xsl:value-of select="date"/>
                </span>
            </td>
        </tr>
    </xsl:template>

    <xsl:template match="index">
        <tr>
            <td class="index">
                <xsl:for-each select="entry">
                    <xsl:sort select="@key"/>
                    <a>
                        <xsl:attribute name="href">
                            <xsl:value-of select="concat('#', @ref)"/>
                        </xsl:attribute>
                        <xsl:value-of select="@key"/>
                    </a>
                </xsl:for-each>
            </td>
        </tr>
    </xsl:template>

    <xsl:template match="resource">
        <xsl:if test="@first">
            <tr>
                <td class="section">
                    <xsl:value-of select="@first"/>
                </td>
            </tr>
        </xsl:if>
        <tr>
            <td class="resourceHeader">
                <a>
                    <xsl:attribute name="id">
                        <xsl:value-of select="@id"/>
                    </xsl:attribute>
                    <xsl:attribute name="name">
                        <xsl:value-of select="@id"/>
                    </xsl:attribute>
                    <span class="type">
                        <xsl:value-of select="@type"/>
                    </span> &#160; <span class="name">
                        <xsl:value-of select="name"/>
                    </span>
                </a>
            </td>
        </tr>
        <tr>
            <th>Address</th>
        </tr>
        <tr>
            <td class="address">
                <xsl:apply-templates select="address"/>
            </td>
        </tr>
        <tr>
            <th>Contact info</th>
        </tr>
        <tr>
            <td class="contactInfos">
                <xsl:choose>
                    <xsl:when test="contactInfos/contactInfo">
                        <xsl:apply-templates select="contactInfos"/>
                    </xsl:when>
                    <xsl:otherwise>
                        <span class="none">No contact info</span>
                    </xsl:otherwise>
                </xsl:choose>
            </td>
        </tr>
        <xsl:if test="roles/role or jobs/job or holders/holder">
            <tr>
                <td>
                    <table class="xrefs">
                        <tr>
                            <xsl:if test="roles/role">
                                <th>Roles</th>
                            </xsl:if>
                            <xsl:if test="jobs/job">
                                <th>Jobs</th>
                            </xsl:if>
                            <xsl:if test="holders/holder">
                                <th>In this position</th>
                            </xsl:if>
                        </tr>
                        <tr>
                            <xsl:if test="roles/role">
                                <td class="roles">
                                    <xsl:apply-templates select="roles"/>
                                </td>
                            </xsl:if>
                            <xsl:if test="jobs/job">
                                <td class="jobs">
                                    <xsl:apply-templates select="jobs"/>
                                </td>
                            </xsl:if>
                            <xsl:if test="holders/holder">
                                <td class="holders">
                                    <xsl:apply-templates select="holders"/>
                                </td>
                            </xsl:if>
                        </tr>
                    </table>
                </td>
            </tr>
        </xsl:if>
        <xsl:if test="members/resource">
            <tr>
                <th>Members</th>
            </tr>
            <tr>
                <td class="members">
                    <xsl:apply-templates select="members"/>
                </td>
            </tr>
        </xsl:if>
    </xsl:template>

    <xsl:template match="addess">
        <table>
            <tr>
                <th>Street address:</th>
                <td colspan="5">
                    <xsl:value-of select="street"/>
                </td>
            </tr>
            <tr>
                <th>City:</th>
                <td>
                    <xsl:value-of select="city"/>
                </td>
                <th>State:</th>
                <td>
                    <xsl:value-of select="state"/>
                </td>
                <th>Zip:</th>
                <td>
                    <xsl:value-of select="code"/>
                </td>
            </tr>
        </table>
    </xsl:template>

    <xsl:template match="contactInfos">
        <table>
            <xsl:apply-templates/>
        </table>
    </xsl:template>

    <xsl:template match="contactInfo">
        <tr>
            <th>
                <xsl:value-of select="@medium"/>
            </th>
            <td>
                <xsl:value-of select="."/>
            </td>
        </tr>
    </xsl:template>

    <xsl:template match="roles">
        <ul>
            <xsl:apply-templates/>
        </ul>
    </xsl:template>

    <xsl:template match="role">
        <li>
            <xsl:value-of select="."/>
        </li>
    </xsl:template>

    <xsl:template match="jobs">
        <ul>
            <xsl:apply-templates/>
        </ul>
    </xsl:template>

    <xsl:template match="job">
        <li>
            <a>
                <xsl:attribute name="href">
                    <xsl:value-of select="concat('#',@ref)"/>
                </xsl:attribute>
                <xsl:value-of select="."/>
            </a>
        </li>
    </xsl:template>

    <xsl:template match="holders">
        <ul>
            <xsl:apply-templates/>
        </ul>
    </xsl:template>

    <xsl:template match="holder">
        <li>
            <a>
                <xsl:attribute name="href">
                    <xsl:value-of select="concat('#',@ref)"/>
                </xsl:attribute>
                <xsl:value-of select="."/>
            </a>
        </li>
    </xsl:template>

    <xsl:template match="members">
        <table>
            <xsl:apply-templates/>
        </table>
    </xsl:template>

    <xsl:template match="footer">
        <tr>
            <td>
                <span class="proprietary">
                    <xsl:value-of select="proprietary"/>
                </span>
                <span class="confidential">
                    <xsl:value-of select="confidential"/>
                </span>
            </td>
        </tr>
    </xsl:template>

</xsl:stylesheet>
